SUBROUTINE GEN_FNPT_3D(LNODE,NODN,NODEID,COORX,&
   FN_NX,FN_NZ,FN_NN,FN_CO,FN_FS,FN_VA,FN_DDL,DOMBL,DOMTR,&
   NFS,XFS,YFS,ZFS,CM)
   USE DOMAINMOD
   IMPLICIT NONE

   TYPE(DOMAINDATA),INTENT(INOUT)::CM
   INTEGER(KIND=4),INTENT(IN)::LNODE,NODN,NODEID(-7:NODN)
   INTEGER(KIND=4),INTENT(IN)::FN_NX,FN_NZ,FN_NN
   REAL(KIND=8),INTENT(IN)::COORX(LNODE),FN_FS(FN_NX,2),FN_DDL
   REAL(KIND=8),INTENT(IN)::FN_CO(FN_NN,2),FN_VA(FN_NN,6)
   REAL(KIND=8),INTENT(IN)::DOMBL(3),DOMTR(3)

   INTEGER(KIND=4),INTENT(OUT)::NFS
   REAL(KIND=8),INTENT(OUT)::XFS(NODEID(-2)),YFS(NODEID(-2)),ZFS(NODEID(-2))

   INTEGER(KIND=4)::I,J,FN_NYS
   REAL(KIND=8)::FN_DY,XLIM

   XLIM=MAXVAL(COORX(NODEID(-2)+1:NODEID(-3)))

   FN_NYS=FLOOR((DOMTR(2)-DOMBL(2))/FN_DDL)+1
   FN_DY=(DOMTR(2)-DOMBL(2))/FN_NYS

   I=FN_NN*(FN_NYS+1)+NODEID(-1)
   WRITE(8,'(" [---] SIZE OF CM VS LNODE ",2I10)')I,LNODE
   CALL CM%INITALL(I)

   NFS=0
   DO I=1,FN_NX
      IF(FN_FS(I,1).GT.XLIM) CYCLE
      DO J=0,FN_NYS
         NFS=NFS+1
         XFS(NFS)=FN_FS(I,1)
         YFS(NFS)=DOMBL(2)+1D0*FN_DY*J
         ZFS(NFS)=FN_FS(I,2)
      ENDDO
   ENDDO

   CM%NP=0
   DO I=1,FN_NN
      IF(FN_CO(I,1).GT.XLIM) CYCLE
      DO J=0,FN_NYS
         CM%NP=CM%NP+1
         IF(CM%NP.GT.CM%NPS)THEN
            WRITE(8,'(" [ERR] ERROR IN SIZE OF ARRAYS IN CM OBJECT")')
            STOP
         ENDIF
         CM%CX(CM%NP)=FN_CO(I,1)
         CM%CY(CM%NP)=DOMBL(2)+1D0*FN_DY*J
         CM%CZ(CM%NP)=FN_CO(I,2)
         CM%PR(CM%NP)=FN_VA(I,1)
         CM%UX(CM%NP)=FN_VA(I,2)
         CM%UY(CM%NP)=0D0
         CM%UZ(CM%NP)=FN_VA(I,4)
      ENDDO
   ENDDO

   WRITE(8,'(" [---] NUM NODE OF FS FNPT SURFACE ",I10)')NFS
   WRITE(8,'(" [---] NUM Y LAYERS ",I10,F15.6)')FN_NYS+1,FN_DY
   WRITE(8,'(" [---] Y LAYER LIM ",2F15.6)')DOMBL(2),DOMBL(2)+FN_NYS*FN_DY
   WRITE(8,'(" [---] X LAYER LIM ",2F15.6)')FN_FS(1,1),XLIM
   WRITE(8,*)
END SUBROUTINE GEN_FNPT_3D
